# basic namespaces
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#> 

# EuroVoc and SKOS-AP/SRC-AP specific namespaces
prefix vb: <http://art.uniroma2.it/ontologies/vocbench#> 
prefix eurovoc: <http://eurovoc.europa.eu/>
prefix euvoc: <http://publications.europa.eu/ontology/euvoc#> 

prefix label: <http://publications.europa.eu/resource/authority/label-type/> 
prefix status: <http://publications.europa.eu/resource/authority/concept-status/> 
prefix align: <http://knowledgeweb.semanticweb.org/heterogeneity/alignment#>

# some popular namespaces
prefix skos: <http://www.w3.org/2004/02/skos/core#> 
prefix skosxl: <http://www.w3.org/2008/05/skos-xl#> 
prefix dct: <http://purl.org/dc/terms/> 
prefix dc: <http://purl.org/dc/elements/1.1/> 
prefix prov: <http://www.w3.org/ns/prov#> 

prefix lemon: <http://lemon-model.net/lemon#> 
prefix lexinfo: <http://www.lexinfo.net/ontology/2.0/lexinfo#> 
prefix lexvo: <http://lexvo.org/ontology#> 

# legacy namespaces
prefix oldevo: <http://eurovoc.europa.eu/schema#> 
prefix thes: <http://purl.org/iso25964/skos-thes#>

# versioning namespaces 
PREFIX dsv: <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX sh: <http://purl.org/skos-history/>
PREFIX xhv: <http://www.w3.org/1999/xhtml/vocab#>

#
# identify concepts deleted with a certain version
#
# (In published vocabularies, the deletion of concepts should be regarded
# as bad practice, because they still may be referenced elsewhere.
# Cosider using owl:deprecated instead)
#
SELECT distinct ?class (?instance AS ?deletedInstance) (str(?prefLabel) AS ?deletedInstanceLabel)
WHERE {
  GRAPH ?versionHistoryGraph {
    # parameters
    VALUES ( ?versionHistoryGraph ?oldVersion ?newVersion ?class ?language ) {
        ( undef 
          undef 
          undef 
          skos:Collection 
          "en"
        )
    }
    # get the current and the previous version as default versions
    ?versionset dsv:currentVersionRecord/xhv:prev/dc:identifier ?previousVersion .
    ?versionset dsv:currentVersionRecord/dc:identifier ?latestVersion .
    # select the versions to actually use
    BIND(coalesce(?oldVersion, ?previousVersion) AS ?oldVersionSelected)
    BIND(coalesce(?newVersion, ?latestVersion) AS ?newVersionSelected)
    # get the delta and via that the relevant graphs
    ?delta a sh:SchemeDelta ;
      sh:deltaFrom/dc:identifier ?oldVersionSelected ;
      sh:deltaTo/dc:identifier ?newVersionSelected ;
      sh:deltaFrom/sh:usingNamedGraph/sd:name ?oldVersionGraph ;
      sh:deltaTo/sh:usingNamedGraph/sd:name ?newVersionGraph .
    ?insertions a sh:SchemeDeltaInsertions ;
      dct:isPartOf ?delta ;
      sh:usingNamedGraph/sd:name ?insertionsGraph .
    ?deletions a sh:SchemeDeltaDeletions ;
      dct:isPartOf ?delta ;
      sh:usingNamedGraph/sd:name ?deletionsGraph .
  }
  # for each deleted instance, a deleted instiation statement must and a prefLabel should exist
  GRAPH ?deletionsGraph {
    ?instance a ?class .
  }
  # ... without anything remaining for this instance in the current version
  FILTER NOT EXISTS {
    GRAPH ?newVersionGraph {
      ?instance ?p []
    }
  }
  # get the label
  GRAPH ?oldVersionGraph {
    ?instance a ?class .
    {
        ?concept skos:prefLabel ?prefLabel .
        
        # restrict output to a certain language
        FILTER (lang(?prefLabel) = "en")
        
    }
  }
}
ORDER BY ?instance